#Область ПрограммныйИнтерфейс

// Преобразовать в табличном документе данные расшифровки, сгенерированные СКД, в их реальные значения
// чтобы расшифровка работала без обработчика событий ОбработкаРасшифровки из ФормаОтчета БСП
//
// Параметры:
//  ТабличныйДокумент	 - ТабличныйДокумент
//  ДанныеРасшифровки	 - ДанныеРасшифровкиКомпоновкиДанных 
//
// На основе https://infostart.ru/1c/reports/639758/
Процедура ПреобразоватьРасшифровкуСКД(ТабличныйДокумент, ДанныеРасшифровки) Экспорт
	
	Перем НомерСтроки, НомерСтолбца, Ячейка, ЭлементРасшифровки, МассивПолейРасшифровки, ЭлементМассива;
	
	Для НомерСтроки = 1 По ТабличныйДокумент.ВысотаТаблицы Цикл
		Для НомерСтолбца = 1 По ТабличныйДокумент.ШиринаТаблицы Цикл
			Ячейка = ТабличныйДокумент.Область(НомерСтроки, НомерСтолбца);

			Если ТипЗнч(Ячейка.Расшифровка) <> Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			
			ЭлементРасшифровки = ДанныеРасшифровки.Элементы[Ячейка.Расшифровка];
			Если ЭлементРасшифровки.ОсновноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение Тогда
				МассивПолейРасшифровки = ЭлементРасшифровки.ПолучитьПоля();
				Для Каждого ЭлементМассива Из МассивПолейРасшифровки Цикл
					Если ТипЗнч(ЭлементМассива) = Тип("ЗначениеПоляРасшифровкиКомпоновкиДанных")
							И Не ЭлементМассива.Иерархия Тогда
						// Для полей нессылочного типа СКД вставляет в расшифровку само значение ячейки,
						// что бесполезно и только мешает
						Если Не ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ЭлементМассива.Значение))	Тогда
							Ячейка.Расшифровка = Неопределено;
						Иначе
							Ячейка.Расшифровка = ЭлементМассива.Значение;
						КонецЕсли;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				Ячейка.Расшифровка = Неопределено;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// Код процедур и функций
#КонецОбласти
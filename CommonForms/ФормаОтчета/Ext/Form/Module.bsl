
&НаКлиенте
&ИзменениеИКонтроль("ПослеВыбораФорматаСохранения")
Процедура ПреРас_ПослеВыбораФорматаСохранения(ВыбранныйЭлемент, Контекст)

	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПолноеИмяФайлаОтчета = Контекст.ПолноеИмяФайлаОтчета;
	Если ВыбранныйЭлемент.Значение <> Неопределено Тогда
		ПолноеИмяФайлаОтчета = ПолноеИмяФайлаОтчета + "." + ВыбранныйЭлемент.Значение;
	КонецЕсли;

	Обработчик = Новый ОписаниеОповещения("СохранитьОтчетПослеСохраненияРезультатаОтчета", ЭтотОбъект,
	ПолноеИмяФайлаОтчета);

	ФорматыСохранения = ФорматыСохраненияОтчета(ПолноеИмяФайлаОтчета, Контекст.ИндексФорматовСохраненияОтчета);
	СвойстваРезультата = НастройкиОтчета.СвойстваРезультата; // см. ВариантыОтчетовСлужебный.СвойстваРезультатаОтчета
#Удаление
	РезультатОтчета = РезультатОтчетаДляСохранения(ОтчетТабличныйДокумент, СвойстваРезультата.Заголовки);
#КонецУдаления
#Вставка
	РезультатОтчета = РезультатОтчетаДляСохранения(ОтчетТабличныйДокумент,
		Новый Структура("Заголовки,АдресДанныеРасшифровки",СвойстваРезультата.Заголовки,ОтчетДанныеРасшифровки));
#КонецВставки
	РезультатОтчета.НачатьЗапись(Обработчик, ПолноеИмяФайлаОтчета, ФорматыСохранения);
КонецПроцедуры

&НаСервереБезКонтекста
&ИзменениеИКонтроль("РезультатОтчетаДляСохранения")
Функция ПреРас_РезультатОтчетаДляСохранения(ОтчетТабличныйДокумент, Заголовки)

#Вставка
	Перем СохрЗаголовки, АдресДанныеРасшифровки, ДанныеРасшифровки;
	Если ТипЗнч(Заголовки) = Тип("Структура") Тогда
		СохрЗаголовки = Заголовки;
		АдресДанныеРасшифровки = Заголовки.АдресДанныеРасшифровки;
		Заголовки = Заголовки.Заголовки;
		Если ЗначениеЗаполнено(АдресДанныеРасшифровки) Тогда
			ДанныеРасшифровки = ПолучитьИзВременногоХранилища(АдресДанныеРасшифровки);
		КонецЕсли;
	КонецЕсли;
#КонецВставки

	РезультатОтчета = СкопироватьТабличныйДокумент(ОтчетТабличныйДокумент);

#Вставка
	ПреРас_ТабличныйДокумент.ПреобразоватьРасшифровкуСКД(РезультатОтчета, ДанныеРасшифровки);
#КонецВставки

	Для Каждого ИндексЗаголовка Из Заголовки Цикл

		СвойстваЗаголовка = ИндексЗаголовка.Значение;

		Если Не СвойстваЗаголовка.ПолеСортируется Тогда
			Продолжить;
		КонецЕсли;

		Область = РезультатОтчета.Область(ИндексЗаголовка.Ключ);
		Область.Картинка = Неопределено;

	КонецЦикла;

#Вставка
	Если СохрЗаголовки <> Неопределено Тогда
		Заголовки = СохрЗаголовки;
	КонецЕсли;
#КонецВставки

	Возврат РезультатОтчета;

КонецФункции
